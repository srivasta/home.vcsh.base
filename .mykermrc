;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
define TTYLINE /dev/ttyS0
define TTYSPEED 38400
define FILE_TYPE binary
define FILE_NAMES literal
define FILE_INCOMPLETE keep
define REMOTE_TERM vt100

set prompt C-Kermit-\v(host)>

; File .mykermrc -- based on ckermod.ini
; File CKERMOD.INI, Sample C-Kermit 5A(190) customization file for OS/2.
;
; This file, which is ONLY A SAMPLE, should be called:
;
;   .mykermrc   (UNIX, OS-9, Aegis)
;   CKERMOD.INI (VMS, OpenVMS, AOS/VS, OS/2, Amiga, Atari ST)
;   ckermod.ini (Stratus VOS)
;
; MODify it to suit your needs and preferences, and install it in the same
; directory as your C-Kermit initialization file.  The design of this file
; lets you fill in a section for each different operating system where you
; run C-Kermit.  This file is executed automatically by the standard C-Kermit
; initialization file, CKERMIT.INI (or .kermrc).
;
; Authors:  Christine Gianone and Frank da Cruz, Columbia University.
; Date:     23 November 1992 for C-Kermit 5A(188).
; Modified: 30 June 1993 for edit 189.
;           23 August 1994 for edit 190.
;           29 November 1994 for installation as .kermrc, based on
;              the file supplied with C-Kermit 5A(190) as ckermod.ini
;              This file was modified on installation to set certain
;              parameters.

ECHO Executing C-Kermit customization file \v(cmdfile)...
;
; ... and then remove the ECHO commands above.

COMMENT - Settings that apply to all the systems I use:
;
set delay 1                     ; I escape back quickly
set dial display on             ; I like to watch C-Kermit dial
set window 4                    ; Use 2 window slots
set block 3                     ; Use 16-bit CRC error checking        
;;set receive packet-length 1000  ; Use 1000-character Kermit packets 
set receive packet-length 740
;; set dial init AT&FS0=0S7=30M0W0Y0E1Q0V1X4\\N0&C1&D2&K3%C0\13\10
;; set dial init ATY0&F1M0E1V1Q0&C1&D2S0=0S7=30S13=1\13\10
set dial init ATE1Q0V1X4M0L0&C1&D2&K3S0=0S7=30W2S95=47

; The remaining commands in this file depend on the script programming
; language.  Here we make sure we have it, then we use it to go to the
; section that applies to the type of system where C-Kermit is running:
;
set take error on               ; Make errors fatal temporarily
check if                        ; Do we have an IF command?
set take error off              ; Yes we do, back to normal

check FORWARD
if success forward \v(system)
else goto \v(system)            ; Go execute system-dependent commands

:UNIX                           ; UNIX, all versions...
set delay 1                     ; I escape back quickly
if > \v(version) 501189 if remote end
set line \m(TTYLINE)
set modem type multitech
set terminal apc off            ; Change to ON if desired
def myconn -
    xif < \v(argc) 2 {def \%1 plymouth}, -
    xif > \v(argc) 3 {end 1 macro \v(macro) can have only 2 arguments}, -
    set modem type multitech, -
    set line \m(TTYLINE), -
   if fail end 1 could not set line, -
   set speed \m(TTYSPEED), -
   if fail end 1 could not set speed, -
   set file type \m(FILE_TYPE), -
   if fail end 1 could not set file type, -
   set file names \m(FILE_NAMES), -
   if fail end 1 could not set name translation, -
   set file incomplete \m(FILE_INCOMPLETE), -
   if fail end 1, -
   for \%i 1 10 1 { -
       echo dialing coins: try number \%i, -
       dial coins, -
       if success break, -
   }, -
   if fail end 1 error while dialing, -
   script ~0 ~n AutoProtocolDetect ~n~n~n~n selection> 1 ocal> conn~s\%1.pilgrim.umass.edu -
ogin: srivasta word: m8119110 ype: \m(REMOTE_TERM) , -
   output \b, -
   script ~0 ~n ocal> set~ssession~spassall ocal> resume, -
   xif def \%2 { script ~0 ~n ]> term~s-r~s~s-l~svar/log/tlog, suspend}-
   else connect
end
def mydial set modem type multitech, -   ; Define a macro for dialing out...
   if fail end 1, -
   set line \m(TTYLINE), -
   if fail end 1, -
   set speed \m(TTYSPEED), -
   if fail end 1, -
   set file type \m(FILE_TYPE), -
   if fail end 1, -
   set file names \m(FILE_NAMES), -
   if fail end 1, -
   set file incomplete \m(FILE_INCOMPLETE), -
   if fail end 1, -
   dial \%1, -
   if fail end 1, -
   connect
end                             ; End of UNIX section


:VMS                            ; VMS and OpenVMS
define on_exit -                ; Print a message when Kermit exits
   echo Returning you to \v(system) now.
if > \v(version) 501189 if remote end
set terminal apc off            ; Change to ON if desired
def mydial set modem telebit, - ; Sample macro for dialing out...
   set line txa5, -
   if fail end 1, -
   set speed 38400, -
   if fail end 1, -
   dial \%1, -
   if fail end 1, -
   connect
end                             ; End of VMS section

:OS/2                           ; OS/2 customizations...
set modem type multitech                 ; I have a Hayes modem
set port com1                   ; on communication port 1
set speed 38400                 ; at this speed
set flow rts/cts                ; It uses hardware flow control
set terminal apc off            ; Change to ON if desired
set server display on           ; Show file display in server mode too
;
; OS/2 character sets, change if necessary.  In OS/2, your current PC code
; page is used automatically as the FILE character-set and as the local half
; of your TERMINAL character-set.
;
set command byte 8              ; Use 8 bits between Kermit and console
set xfer char latin1            ; Use Latin-1 for text file transfer
set term char latin1            ; And use Latin-1 during CONNECT mode
;
; Macro for "ANSI" terminal emulation
;
define ANSI set term type ansi

; Macro for setting terminal rows
;
define ROWS run mode co80\{44}\%1

echo { See OS/2 section for sample key mappings and screen colors.}
;
; Sample OS/2 key redefinitions.  As distributed, they are
; "commented out" by the leading semicolon.  To "uncomment" a command,
; remove the semicolon from the left margin.  Add, remove, or change
; key mapping commands to suit your needs and preferences.
; See pages 428-431 of "Using C-Kermit" for details, plus CKERMIT.UPD,
; and see CKOVTK2.INI for a complete VT220 key mapping.
;
; set key \27 \96               ; Swap the Escape and Accent-Grave keys
; set key \96 \27               ; ...during CONNECT mode.
; set key \127 \8               ; Backspace key sends BS rather than DEL.

COMMENT - Terminal emulation screen colors:
;
; TERMINAL-SCREEN   = Terminal emulation screen (synonym: "normal")
; BORDER            = Border around terminal screen (fullscreen sessions only)
; HELP-SCREEN       = Popup help screens
; STATUS-LINE       = Status line
; UNDERLINED-TEXT   = Color for simulating underlined text
;
COMMENT - Sample color setup, change to suit your preferences...
set terminal color border green                ; green border
set terminal color terminal-screen blue lgray  ; blue on light gray
set terminal color underline black lgray       ; black on light gray
set terminal color status green blue           ; green on blue
set terminal color help white cyan             ; white on cyan

; COPY command...
define  copy if < argc 2 fatal {Copy what?},-
  if < argc 3 fatal {Copy \%1 to what?},-
  run copy \%1 \%2

COMMENT - ALTERNATIVE DIAL PROCEDURE
; Sample macro showing how to add a new modem type, in this case
; Practical Peripherals PM14400FXMT - see CKERMIT.UPD for discussion.
define PPDIAL -
  set modem type multitech, - ; Build on basic Hayes modem type.
  set port 1, -      ; Change or remove this if necessary.
  set flow none, -
  set speed 57600, -
  set dial speed-matching off, -
  set dial init ATQ0W1S95=47&D2X4S2=43N1S37=11&K3&Q5S36=7S46=2S82=128\13, -
  dial \%1, -
  if success set flow rts, -
  else end 1

COMMENT - XSEND macro sends a directory tree.  Read XSEND.CMD for details.
; Arguments:
; 1. Pathname, such as C:\\ (backslashes must be doubled!)
; 2. Filespec, such as *.*
;
define XSEND rexx call \v(startup)xsend.cmd '\%1 \%2'

COMMENT - UPDATES displays CKERMIT.INF file with Viewer
define updates run view ckermit.inf

COMMENT - Access to external protocols
;
; These are commented out.  If you need them, and if you have the
; software (M2ZMODEM or P), uncomment the appropriate group of commands.
; Read the CKERMIT.UPD file for further information.

; M2ZMODEM program, external protocol
; take \m(_inidir)m2zmodem.ini

; P v.1.01 program, external protocol
; take \m(_inidir)p101.ini

; P v.2.00 program, external protocol
; take \m(_inidir)p200.ini

; CEXYZ/2 program, external protocol
; take \m(_inidir)cexyz2.ini
end                             ; End of OS/2 section

:OS9/68K                        ; OS-9/68000
if > \v(version) 501189 if remote end
set line /t3                    ; I want to use /t3 at 9600 bps
xif success { -
    set speed 9600, -
    echo { Communications line set to /t3, 9600 bps} -
}
end                             ; End of OS-9 section

:AOS/VS                         ; Data General AOS/VS
if > \v(version) 501189 if remote end
def mydial set modem type multitech, -   ; Macro for dialing out with Hayes modem...
   set line @con3, -            ; Only works if modem is on @con3
   if fail end 1, -
   set speed 2400, -            ; and is set for 2400bps
   if fail end 1, -
   dial \%1, -
   if fail end 1, -
   connect
end

; And so on, you get the idea...
; Fill in the sections that apply to you.

:Stratus_VOS			; Stratus VOS
:Amiga                          ; Commodore Amiga
:Atari_ST                       ; Atari ST
:Macintosh                      ; Apple Macintosh
:unknown                        ; Others

; (End of CKERMOD.INI)
